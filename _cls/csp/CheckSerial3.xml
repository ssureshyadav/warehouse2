<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="20">
<Class name="csp.CheckSerial3">
<Description>
Проверка серийных номеров прямо из отчета о ремонте</Description>
<Super>%CSP.Page</Super>
<TimeCreated>63125,11046.00817</TimeCreated>

<Method name="OnPreHTTP">
<ClassMethod>1</ClassMethod>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
 
 #;  бывает что при открытии нового окна каша запускает новую сессию,
 #;  соответственно пользователь залогинился 
 #;  а в новом окне ему отлуп "залогиньтесь"
 #;  поэтому передаю ID сессии и в случае если каша пыталась запустить новую сессию 
 #;  я её завершаю и восстанавливаю из указанного ID
 if %session.NewSession {
    s sid=%request.Get("sessionid",1)
    s %session.EndSession=1, %session=%session.GetSession(sid)
 }
 
 Q 1
]]></Implementation>
</Method>

<Method name="OnPage">
<Description>
Вывод содержимого </Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
 
 #define g(%name) $g(%request.Data(%name,1))
 
 
 &html<<html>
 <head>
    <meta http-equiv="content-type" content="text/html; charset=windows-1251" />
 <title> Проверка действительности серийных номеров </title>
 <link href="style.css" type=text/css rel=stylesheet>
<script language="javascript" type="text/javascript">
var sessionid="#(%session.SessionId)#";

function s( currentrecord ) {
    var mdo = "center:yes;status:no;dialogHeight:600px;dialogWidth:800px;resizable:yes;" //modal dialog options
    var url = "garant.csp?sessionid="+sessionid+"&GarantId="+currentrecord; 
    ok=window.showModalDialog( url, currentrecord+";Common.Dictionary2", mdo );
}
</script>
</head><body>>
     s docid= $g( %request.Data("id",1) )
     d ..ShowDocData( docid )
 
 &html<</body></html>>
 Quit $$$OK
]]></Implementation>
</Method>

<Method name="ShowDocData">
<Description>
Получение проверяемого списка из документа</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>docid:%String=""</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 
 #; Common.Dictionary2 - Справочник гарантийных талонов
 s glob=$NA(^Common.Dictionary2I("serialInd")) ;прямой доступ к глобали индекса по серийному номеру
 

 #; DISPLAYLIST для свойства актуальность (Property9194)
 s pid="Common.Dictionary2||Property9194" 
 d ##class(Docs.Action2).GetDisplayListArray(pid,.Property9194Array)

 &html<<br><br><table border=1 cellpadding=6 cellspacing=0>
 <tr><td>№ п/п
     <td>Серийный номер
     <td>Актуальность
     <td>Наличие заявки
     <td>Модель
     <td>Примечание
     <td>Дубль серийного номера
 </tr>>
 
 s sql="Select serial From Docs.OtchetItems Where Doc = ?"
 s rs=##class(%ResultSet).%New()
 s sc=rs.Prepare(sql)
 s sc=rs.Execute( docid )
 s CountLines = 0 ;количество строк
 
 for { Q:'rs.Next()
 
    s serial= $g( rs.Data("serial") )  if serial="" continue
    s (garantStr,actual,comment,Model,HasRequest)="Не найден"
    s ID="", color="#FFFFFF", SearchString = " "_$ZCVT(serial,"U")
    i $I(CountLines)    ;плюс строка
    
    //если в индексе справочника гарантийных есть такой серийный
    if '$D(@glob@( SearchString )) continue 
    s ID = $O(@glob@( SearchString, "" )) ;id гарантийного талона
    if ( ID = "" ) continue
    
    #;= ##class(Common.Dictionary2).GetPropertyValue(ID,"Property9194",ListIndex("Property9194"),.DataLocation)
    s Property9194 = ##class(Common.Dictionary2).Property9194GetStored( ID )
    s:Property9194=2 color="#DEDEDE"
    s actual=$G( Property9194Array( Property9194 ) ) ;display value
    
    s garant=$LG( @glob@(SearchString,ID),2 ) ;в индексе сразу хранится номер гарантийного талона
    
    s garant="<a href=""javascript:s('"_ID_"')"">"_garant_"</a>"
    
    
    s comment = ##class(Common.Dictionary2).CommentGetStored( ID )
    s Model = ##class(Common.Dictionary2).tovarGetStored( ID ) 
    s remont1 = ##class(Common.Dictionary2).remont1GetStored(  ID  )
    s HasRequest="нет" s:remont1'="" HasRequest="<b>есть</b>"
    
    s shtml = serial 
    if $G(ID)'="" {
         s shtml = "<a href=""javascript:s('"_$G(ID)_"')"">"_serial_"</a>"
    }     
    
    &html<<tr bgcolor='#(color)#'>
    <td>#(CountLines)#
    <td>#(shtml)#
    <td>#(actual)#
    <td>#(HasRequest)#
    <td>#(Model)#
    <td>#(comment)#&nbsp
    <td>#( $G(Serials(SearchString)) )# &nbsp;
    </tr>>    
    
    #; в какой строке нашли?
    if '$D( Serials( SearchString ) ) s Serials( SearchString ) = +$G(CountLines)
    
 } d rs.Close() k rs
 
 &html<</table>>
 
 Quit ""
]]></Implementation>
</Method>
</Class>
</Export>
