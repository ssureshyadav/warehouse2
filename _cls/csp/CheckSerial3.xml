<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="20">
<Class name="csp.CheckSerial3">
<Description>
Проверка серийных номеров прямо из отчета о ремонте</Description>
<Super>%CSP.Page</Super>
<TimeCreated>63125,11046.00817</TimeCreated>

<Parameter name="DOMAIN">
<Default>csp</Default>
</Parameter>

<Method name="OnPreHTTP">
<ClassMethod>1</ClassMethod>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
 
 #;  бывает что при открытии нового окна каша запускает новую сессию,
 #;  соответственно пользователь залогинился 
 #;  а в новом окне ему отлуп "залогиньтесь"
 #;  поэтому передаю ID сессии и в случае если каша пыталась запустить новую сессию 
 #;  я её завершаю и восстанавливаю из указанного ID
 if %session.NewSession {
    s sid=%request.Get("sessionid",1)
    s %session.EndSession=1, %session=%session.GetSession(sid)
 }
 
 Q 1
]]></Implementation>
</Method>

<Method name="OnPage">
<Description>
Вывод содержимого </Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
 
 #define g(%name) $g(%request.Data(%name,1))
  
 &html<<!doctype html><html lang="ru">
 <head>
    <meta http-equiv="content-type" content="text/html; charset=windows-1251" />
 <title> Проверка действительности серийных номеров </title>
 <link href="style.css" type=text/css rel=stylesheet>
</head><body>
<table border=1 cellpadding=6 cellspacing=0>
 <thead><tr>
    <td>№ п/п
    <td>Серийный номер
    <td>Актуальность
    <td>Наличие заявки
    <td>Модель
    <td>Примечание
    <td>Дубль серийного номера
 </tr></thead><tbody>>
 
     s doccls = $$$g("cls"), docid= $$$g("id")
     d ..ShowDocData( doccls, docid )
     
 &html<</tbody>
 </table>
 
 <script type="text/javascript">
var sessionid="#(%session.SessionId)#";

function s( currentrecord ) {
    var mdo = "center:yes;status:no;dialogHeight:600px;dialogWidth:800px;resizable:yes;" //modal dialog options
    var url = "garant.csp?sessionid="+sessionid+"&GarantId="+currentrecord; 
    ok=window.showModalDialog( url, currentrecord+";Common.Dictionary2", mdo );
}
</script>

 </body></html>>
 Quit $$$OK
]]></Implementation>
</Method>

<Method name="ShowDocData">
<Description>
Получение проверяемого списка из документа</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>doccls:%String,docid:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
 
 s doccls=$g( doccls ), docid = $g( docid )
 s allow = $lb("Docs.Act","Docs.Otchet")
 if '$lf( allow, doccls ) {
     w "<h2>",$$$Text("Неправильный тип документа. Обратитесь к разработчику"),"</h2>"
     Q ""
 }

 #; ПОДГОТОВКА ЗАПРОСА
 if doccls = "Docs.Otchet" {
   s sql="Select serial From Docs.OtchetItems Where Doc = ?"
 } else {
   s sql="Select Serial as serial From Docs.Act Where ID = ?"  
 }
 
 s rs=##class(%ResultSet).%New()
 s sc=rs.Prepare(sql) ;w "Prepare: ",sc
 s sc=rs.Execute( docid ) ;w "Execute: ",sc
  
 #; Вспомогательные данные для отображения 
 s CountLines = 0 ;количество строк
 #; Common.Dictionary2 - Справочник гарантийных талонов
 s glob=$NA(^Common.Dictionary2I("serialInd")) ;прямой доступ к глобали индекса по серийному номеру
 
 #; DISPLAYLIST для свойства актуальность (Property9194)
 s pid="Common.Dictionary2||Property9194" 
 d ##class(Docs.Action2).GetDisplayListArray(pid,.Property9194Array)
 
 for { Q:'rs.Next()

    s serial= $g( rs.Data( "serial" ) )  if serial="" continue
    s shtml = serial ;выводим просто как текст
    s (Property9194, actual, garant, comment, Model, remont1, HasRequest)="Не найден" ;значения по умолчанию
    s ID="", color="#FFFFFF", SearchString = " "_$ZCVT(serial,"U")
    i $I(CountLines)    ;плюс строка
    
    #;если в индексе справочника гарантийных есть такой серийный
    s ID = $O(@glob@( SearchString, "" )) ;id гарантийного талона
    
    if ( ID '= "" )  { ;серийный номер есть в индексе
        
        s Property9194 = ##class(Common.Dictionary2).Property9194GetStored( ID )
        s:Property9194=2 color="#DEDEDE"
        s actual=$G( Property9194Array( Property9194 ) ) ;display value
    
        s garant=$LG( @glob@(SearchString,ID),2 ) ;в индексе сразу хранится номер гарантийного талона
        , garant="<a href=""javascript:s('"_ID_"')"">"_garant_"</a>" ;оформляем
    
        s comment = ##class(Common.Dictionary2).CommentGetStored( ID )
        s Model = ##class(Common.Dictionary2).tovarGetStored( ID ) 
        s remont1 = ##class(Common.Dictionary2).remont1GetStored(  ID  )
        s HasRequest="нет" s:remont1'="" HasRequest="<b>есть</b>"
        s shtml = "<a href=""javascript:s('"_$G(ID)_"')"">"_serial_"</a>"
        
    }
    
    &html<<tr bgcolor='#(color)#'>
    <td>#(CountLines)#
    <td>#(shtml)#
    <td>#(actual)#
    <td>#(HasRequest)#
    <td>#(Model)#
    <td>#(comment)#&nbsp
    <td>#( $G(Serials(SearchString)) )# &nbsp;
    </tr>>    
    
    #; в какой строке нашли?
    if '$D( Serials( SearchString ) ) s Serials( SearchString ) = +$G(CountLines)
    
 } d rs.Close() k rs
 
 
 
 Quit ""
]]></Implementation>
</Method>
</Class>
</Export>
