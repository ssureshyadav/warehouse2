<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="20">
<Class name="csp.StoreModal4">
<Description>
Отображение дерева хранилища в диалоге</Description>
<Super>%CSP.Page</Super>
<TimeCreated>62294,80941.688004</TimeCreated>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	&html<<html><head>
	<style type="text/css">
	 #depotTree {height:350px; overflow:auto;padding:3px; border:1px solid #777777;}
	 #depotTree li {list-style-type: none;}
	 #depotTree li.root {margin-left:0px;}
	 .percent {display:block;width:100px;border:1px solid green;margin:0px;padding:0px;height:5px;line-height:5px;overflow:hidden;}
	 .percent div {background-color:green;border:0px none;}
	</style>
	</head><body>
		<table id='depotExplorer'>
			<tr><td>>
	d ..wHtmlTree(1)
	&html<</div>
	</td><td>
		<div id='depotDetail'>
	</div></tr>
	</table>
	</body></html>>
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="DepotTree">
<Description>
Собираем дерево хранилища, сортировка по наименованию</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>gln:%String=""</FormalSpec>
<Implementation><![CDATA[
	s depot=@gln, root=$na(@gln@("root")), data=$na(@gln@("data"))
	&sql(Declare drs CURSOR For 
    	Select ID,ParentID,Name,Percent
    		Into :id,:parent,:name,:percent
    	From Store.Address
    	Where Depot=:depot
    	-- Сначала узлы верхнего уровня по наименованию --
    	Order By ParentID,Name
    ) &sql(Open drs)
    for { &sql(FETCH drs) Q:SQLCODE
    	if parent="" {
    		s @root@(%ROWCOUNT)=id	
    		s @data@(id,"root")=1
    	} else {
	    	s @data@(parent,"child",%ROWCOUNT)=id
    	}	
    	s @data@(id)=name, @data@(id,"percent")=percent
    } &sql(Close drs)
  	Q ""
]]></Implementation>
</Method>

<Method name="gTempGln">
<Description>
Формирует уникальное имя глобали</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	s uniq=$P_"^"_$zu(188)	
	k ^CacheTemp.Temp(uniq)
	Q $na(^CacheTemp.Temp(uniq))
]]></Implementation>
</Method>

<Method name="wHtmlTree">
<Description>
Формирует дерево в виде вложенных списков</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>depot:%String</FormalSpec>
<Implementation><![CDATA[
	s gln=..gTempGln() s @gln=depot ;заносим параметр в массив
	d ..DepotTree(gln) ;заполняем
	s gld=$na(@gln@("data")), glr=$na(@gln@("root"))
	w "<ul id='depotTree'>"
		d ..wHtmlTreeNodes(gld,glr)
	w "</ul>"
]]></Implementation>
</Method>

<Method name="wHtmlTreeNodes">
<Description>
Выводит узлы по списку из массива rgln, данные об узлах в из rgln</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>dgln:%String,rgln:%String</FormalSpec>
<Implementation><![CDATA[
	s i="" for { s i=$o(@rgln@(i)) q:i=""
		s id=$g(@rgln@(i)),root=$g(@dgln@(id,"root"))
		s name=$g(@dgln@(id)),percent=$g(@dgln@(id,"percent"))
		w "<li id='node'"
		w:root "class='root'"
		w ">"
		w "<div title='",percent,"%'>",name,"</div>"
		w "<div class='percent'>"
			if percent>0 {
				w "<div style='"
				if percent>100 s percent=100 w "background-color:#ff3333;"
				w "width:",percent,"%;"
				w "'/>"
			}
		w "</div>"
		if $d(@dgln@(id,"child")) {
			w "<ul>"
			d ..wHtmlTreeNodes(dgln,$na(@dgln@(id,"child")))
			w "</ul>"
		}
		w "</li>"
	}
]]></Implementation>
</Method>
</Class>
</Export>
