<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="20">
<Class name="csp.ReqWar">
<Description><![CDATA[
Request - Warranty Card
Просмотр гарантийных талонов по заявке
Заявка <class>Operation.Instructions</class>  с Type=10
, содержит строки <class>Operation.InstructionsItems</class> 
c полем <var>garant</var> в котором хранится номер гарантийного талона
Задача - показывать связанные гарантиные талоны по ID заявки]]></Description>
<Super>json.service2</Super>
<TimeCreated>62582,60240.213642</TimeCreated>

<Parameter name="CLASSNAME">
<Type>%String</Type>
<Default>Common.Dictionary2</Default>
</Parameter>

<Parameter name="FIELDS">
<Type>%String</Type>
<Default>Name as sn,serial as gn,Comment as cm,remont1 as rm,Property9194 as st</Default>
</Parameter>

<Parameter name="FTYPES">
<Type>%String</Type>
<Default>%String,%String,%String</Default>
</Parameter>

<Method name="OnPage">
<Description>
Вывод на страницу </Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
  m rq=%request.Data  ;query arguments
  s oper=$G(rq("oper",1)), json=$d(rq("json")) 
  ;Q:oper="add" ..Add(.rq) 
  ;Q:oper="edit" ..Edit(.rq) 
  ;Q:oper="del" ..Delete(.rq)
  Q:json ..Out(.rq)  ;Просмотр записей
  d ..wUI(.rq)
  Quit $$$OK
]]></Implementation>
</Method>

<Method name="Out">
<Description><![CDATA[
Вывод первой или указанной страницы
<var>rq</var> %request.Data]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&rq:%String=""]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
 s classname=..#CLASSNAME
 #; по коду Operation.Instruction
 #; отбираем Operation.InstructionItems
 s doc=$g(rq("id",1)), in="",allrows=0 
  
 #; Один раз отбираем id по гарантийные номерам из строкам документа
 #; Обнаружил, что инструкция с id=27220 содержит строки 
 #; с повторами гарантийных 
 &sql(Declare inrs Cursor For 
 	Select Distinct cd2.ID Into :gid 
 	From Operation.InstructionsItems oii Inner JOIN Common.Dictionary2 cd2
 		On oii.garant=cd2.Name
 	Where oii.Doc=:doc
 ) &sql(Open inrs)
 for { &sql(Fetch inrs) Q:SQLCODE'=0
 	s allrows=allrows+1
	s:allrows>1 in=in_","
	s in=in_"'"_gid_"'"
 } &sql(Close inrs)
 
 #; сливаем в параметры в запрос
 s rq("where")=" WHERE ID In ("_in_")"
 
 #;уже посчитали
 #;s allrows=..GetCount(.rq) ;количество строк с такими параметрами запроса
 
 s rows=+$G(rq("rows",1),100) ;вывести строк на страницу, по умолчанию 100
 s:rows<1 rows=100 ;контроль количества строк на страницу
 
 s page=+$G(rq("page",1),1) ;номер запрашиваемой страницы, по умолчанию первой
 s:page<1 page=1 ;контроль номера страницы 
 
 s pages=0 ;всего страниц данных
 s:allrows>0 pages=allrows\rows ; если есть строки 
 s:allrows#rows>0 pages=pages+1 ;непоместившиеся строки на след страницу
 s:page>pages page=pages ;нельзя вывести страницу больше чем есть

 #;диапазон выводимых строк
 s lastRow=rows*page ;последняя запись которую надо вывести с записи
 s firstRow=lastRow-rows ; начиная с записи
 
 s sql="SELECT ID FROM "_..#CLASSNAME_$g(rq("where"))_..GetOrder(.rq) 
 k qp s qp=0 m qp=rq("where","qp")
 #;w !,"sql: ",sql
 #;zw qp
 
 
 #; Переходим на универсальный интерфейс к динамическим запросам
 s sc=1, rs=##class(json.xvrs).GetResultSet(sql,.qp,.sc)
 if 'sc { w sc Q $$$OK  }
 s first=1 
 w "{"
 #; w """userData"":",..QuoteJS(sql),","
 w """page"":""",page,""""
 w ",""total"":""",pages,""",""records"":""",allrows,""",""rows"":["
 while rs.Next() {
   s i=$i(i) ;счетчик выведенных строк
   if i<=firstRow continue ;Не дошли до первой записи
   if i>lastRow quit  ;прошли последнюю запись
   w:'first "," s:first first=0 ;запятая перед каждым объектом кроме первого
   d ..wJson(rs.Get("ID")) ;вывод объекта 
 }
 d rs.Close() k rs
 w "]}"
 Q $$$OK
]]></Implementation>
</Method>

<Method name="wJson">
<Description>
Свой вывод объектов</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
 w "{""id"":""",id,""""
 s sn=##class(Common.Dictionary2).NameGetStored(id)
 w ",""sn"":""",$$qt(sn),""""	
 s gn=##class(Common.Dictionary2).serialGetStored(id)
 w ",""gn"":""",$$qt(gn),""""	
 s cm=##class(Common.Dictionary2).CommentGetStored(id) 
 w ",""cm"":""",$$qt(cm),""""	 
 s st=##class(Common.Dictionary2).Property9194GetStored(id) 
 s stnm=##class(Common.Dictionary2).Property9194LogicalToDisplay(st) 
 w ",""st"":""",$$qt(stnm),""""	 
 w "}"
 Q $$$OK
	
qt(str)
	s qstr=..QuoteJS(str), uqstr=$e(qstr,2,$l(qstr)-1)
	Q uqstr
]]></Implementation>
</Method>

<Method name="wUI">
<Description>
Интерфейс для просмотра</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&rq:%String]]></FormalSpec>
<Implementation><![CDATA[
 s iid=$g(rq("id",1)),(inm,idt)=""
 #SQLCompile Select=Display
 &sql(Select Name,InstDate Into :inm,:idt From Operation.Instructions
 	Where ID=:iid
 )
 
 &html<<!DOCTYPE html>
<html><head>
<script type='text/javascript' src='js/jquery-1.6.4.min.js'></script>
<script type='text/javascript' src='js/jquery-ui-1.8.16.custom.min.js'></script>
<script type='text/javascript' src='js/i18n/grid.locale-ru_win1251.js'></script>
<script type='text/javascript' src='js/jquery.jqGrid-4.1.2.min.js'></script>
<script type="text/javascript">
$(function(){
	var GRID="#gg",BAR="#gb", id='#(iid)#';
	
	var fn_openTalon=function(id){
		window.showModalDialog("garant.csp?GarantId="+id
	   	,id+";Common.Dictionary2","center:yes;status:no;dialogHeight:600px;dialogWidth:800px;resizable:yes;");	
	};
	
	$( GRID ).jqGrid({ //Таблица с типами  документов
	 url: ""+window.location
	 ,mtype: "POST"
	 ,datatype: "json", jsonReader : { repeatitems: false }
	 ,caption: "Гарантийные талоны заявки #(inm)# #(idt)#"
	 ,colModel: [
	  {name:'id', hidden:true}
      , {name:'sn',label:'Серийный', width:150}
      , {name:'gn',label:'Гарантийный', width:150}
      , {name:'st',label:'Актуальность', width:150}
      , {name:'cm',label:'Комментарий', width:250}
  	 ]
  	 , postData: {json:1, id: id}
  	 , pager: BAR
  	 , viewrecords:true
  	 , height: 300
  	 , width: 700 
  	 , hidegrid: false
     , gridview:true ,rownumbers:true,viewrecords:true,rowNum:100
     , hoverrows:true ,scroll:1 
     , ondblClickRow: fn_openTalon
  	 
	})
	.jqGrid('navGrid',BAR, 
       {add:false,edit:false,del: false	,view:false,search:false}
    )
    .jqGrid('navButtonAdd',BAR,{
		caption:"Просмотр"
		, title:"Просмотр выбранного талона ( двойной клик )"
		, buttonicon:"ui-icon-document"
		, onClickButton: function(){
			var id=$( GRID ).jqGrid("getGridParam","selrow");
			if (!id) {alert("Выберите талон"); return;} 
			fn_openTalon(id);
		}
		, position: "first"
	})
    ;
	
	$( BAR +"_center").remove();
	$( BAR +"_right").css("width","30%");
	$( BAR +"_left").css("width","70%");

});


</script>
<link rel='stylesheet' type='text/css' href='css/redmond/jquery-ui-1.8.16.custom.css'/>
<link rel='stylesheet' type='text/css' href='css/ui.jqgrid.css'/>
<style type='text/css'>
	.ui-widget {font-size:.8em;}
	.ui-state-default {font-weight:normal}
	caption {text-align: left; }
</style>
</head><body><table id="gg"></table><div id="gb"></div></body></html>>
]]></Implementation>
</Method>
</Class>
</Export>
